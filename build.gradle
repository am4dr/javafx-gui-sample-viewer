plugins {
    id 'java-library'
    id 'maven-publish'

    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'com.jfrog.bintray' version '1.8.3'
}

repositories {
    jcenter()
}

group = 'com.github.am4dr.javafx'
version = '0.4.1'

ext {
    jdkVersion = JavaVersion.VERSION_1_10
    junit_version = '5.2.0'
    libraries = [
            'junit_api': 'org.junit.jupiter:junit-jupiter-api:'+junit_version,
            'junit_runtime': 'org.junit.jupiter:junit-jupiter-engine:'+junit_version,
            'jimfs': 'com.google.jimfs:jimfs:1.1',
    ]
    samples = ['colorful-buttons', 'colorful-buttons_with-sourceset-separation'].collect { project ":$it" }
}

dependencies {
    testImplementation libraries.jimfs
    testImplementation libraries.junit_api
    testRuntime libraries.junit_runtime
}
test {
    useJUnitPlatform()
}

sourceSets {
    uiSample
}
dependencies {
    uiSampleCompile sourceSets.main.output
}
task runUiSamples(type: JavaExec) {
    classpath = sourceSets.uiSample.runtimeClasspath
    main = 'com.github.am4dr.javafx.sample_viewer.ui.Launcher'
    args = ['com.github.am4dr.javafx.sample_viewer.example.ControlSamples']
}

javadoc {
    options {
        links 'https://docs.oracle.com/javase/10/docs/api'
        locale 'en_US'
        addBooleanOption('html5', true)
    }
}
task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}
task javadocJar(type: Jar) {
    from javadoc
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'gui-sample-viewer'
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}
configurations {
    jarForBintrayUpload
}
artifacts {
    jarForBintrayUpload jar
    jarForBintrayUpload sourceJar
    jarForBintrayUpload javadocJar
}
bintray {
    user = bintrayUsername
    key = bintrayApiKey
    pkg {
        repo = 'maven'
        name = 'javafx-gui-sample-viewer'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/am4dr/javafx-gui-sample-viewer.git'
        websiteUrl = 'https://github.com/am4dr/javafx-gui-sample-viewer'
        githubRepo = 'am4dr/javafx-gui-sample-viewer'
        version {
            name = project.version
            vcsTag = project.version
            released  = new Date()
        }
    }
    publications = ['mavenJava']
}


configure(samples) {
    apply plugin: 'java-library'
    sourceCompatibility = jdkVersion

    dependencies {
        api project(':')
    }
}
